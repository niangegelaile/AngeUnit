apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt'
apply plugin: 'com.squareup.sqldelight'
android {

    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    //默认设置
    defaultConfig {
        applicationId "com.example.ange.angeunit"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 1
        versionName "1.0"
        ndk {
            // 设置支持的SO库架构
            abiFilters 'armeabi' //, 'x86',  'x86_64'//, 'arm64-v8a','armeabi-v7a'
        }
    }

    //签名配置
    signingConfigs{
        project_prod{
            keyAlias KEY_AlIAS
            keyPassword KEY_PASSWORD
            storeFile new File(rootProject.rootDir,"ange.jks")
            storePassword STORE_PASSWORD
        }
    }

    //so包资源放置位置
    sourceSets {
        main {
            jniLibs.srcDirs = ['libs']
        }
    }

    //编译类型
    buildTypes {
        //调试
        debug{
            minifyEnabled false
            signingConfig signingConfigs.project_prod
        }
        //正式
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.project_prod
        }
    }

    productFlavors{
        prod{
            //把变量塞到androidmanifest文件里
            manifestPlaceholders=new HashMap<>()
            manifestPlaceholders.put("BAIDU_APIKEY","$BAIDU_APIKEY")
            //把变量塞到buildConfig里
            buildConfigField("long","BAIDU_TRACE_SERVICE_ID","$BAIDU_TRACE_SERVICE_ID")
        }
    }

    packagingOptions {
        exclude 'META-INF/services/javax.annotation.processing.Processor'
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:1.10.19'
    testCompile 'org.robolectric:robolectric:3.0'
    compile 'com.android.support:appcompat-v7:24.2.0'
    compile 'com.google.dagger:dagger:2.0.2'
    compile 'javax.annotation:jsr250-api:1.0'
    apt 'com.google.dagger:dagger-compiler:2.0.2'
    compile 'com.squareup.okhttp3:okhttp:3.4.1'
    compile 'com.squareup.retrofit2:retrofit:2.0.2'
    compile 'com.squareup.retrofit2:converter-gson:2.0.0'
    compile 'com.squareup.retrofit2:adapter-rxjava:2.0.0'
    compile 'io.reactivex:rxjava:1.2.0'
    compile 'io.reactivex:rxandroid:1.2.1'
    compile 'com.jakewharton:butterknife:8.4.0'
    apt 'com.jakewharton:butterknife-compiler:8.4.0'
    compile 'com.squareup.sqlbrite:sqlbrite:0.7.0'
    provided 'com.ryanharter.auto.value:auto-value-parcel:0.2.4-rc2'
    apt 'com.google.auto.value:auto-value:1.3'
    compile 'com.readystatesoftware.systembartint:systembartint:1.0.3'
    compile project(':baidu_map_sdk')
    compile project(':multi-image-selector')
}
